buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath 'au.com.dius.pact.provider:gradle:4.3.11'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.6.3"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath 'net.idlestate:gradle-download-dependencies-plugin:1.+'
    }
}
apply plugin: "java"
apply plugin: "au.com.dius.pact"
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "net.idlestate.download-dependencies"

//plugins {
//    id 'org.springframework.boot' version '2.7.1'
//    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
//    id 'java'
//}

group = 'au.com.dius.pactworkshop'
version = '0.0.1'
sourceCompatibility = "16"
targetCompatibility = "16"

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

downloadDependencies {
    localRepository = file( './deps' )
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:3.1.3"
    }
}

dependencies {
    //Spring
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    //Pact
    testImplementation("au.com.dius.pact.provider:junit5:4.3.11")

    //DB
    runtimeOnly 'com.h2database:h2'

    //Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    //Swager validator
    implementation 'com.atlassian.oai:swagger-request-validator-mockmvc:2.28.2'
    implementation 'com.atlassian.oai:swagger-request-validator-restassured:2.28.2'
    implementation 'com.atlassian.oai:swagger-request-validator-pact:2.28.2'
    implementation 'com.atlassian.oai:swagger-request-validator-core:2.28.2'


}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

test {
    useJUnitPlatform()

    if (System.getProperty('pactPublishResults') == 'true') {
        systemProperty 'pact.provider.version', version + "+" + getGitHash()
        systemProperty 'pact.provider.tag', getGitBranch()
        systemProperty 'pact.verifier.publishResults', 'true'
    }
}
